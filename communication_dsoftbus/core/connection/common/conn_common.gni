# Copyright (c) 2021 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("../../../dsoftbus.gni")

conn_common_inc = [
  "$dsoftbus_root_path/core/connection/interface",
  "$dsoftbus_root_path/core/connection/common/include",
  "$dsoftbus_root_path/core/connection/manager",
  "$dsoftbus_root_path/core/transmission/trans_channel/proxy/include",
]
conn_common_src =
    [ "$dsoftbus_root_path/core/connection/common/src/softbus_conn_common.c" ]

if (dsoftbus_feature_conn_tcp_comm) {
  conn_common_src += [
    "$dsoftbus_root_path/core/connection/common/src/softbus_base_listener.c",
    "$dsoftbus_root_path/core/connection/common/src/softbus_datahead_transform.c",
    "$dsoftbus_root_path/core/connection/common/src/softbus_socket.c",
    "$dsoftbus_root_path/core/connection/common/src/softbus_tcp_socket.c",
  ]
  if (defined(ohos_lite) && ohos_kernel_type == "liteos_m") {
    conn_common_src += [ "$dsoftbus_root_path/core/connection/common/src/softbus_select_event_implement.c" ]
  } else {
    conn_common_src += [ "$dsoftbus_root_path/core/connection/common/src/softbus_epoll_event_implement.c" ]
  }
  native_source_path = rebase_path("$dsoftbus_root_path")
  newip_dir = "dsoftbus_enhance/components/newip/network"
  newip_enhanced = exec_script("$dsoftbus_root_path/check_sub_module.py",
                               [
                                 "$native_source_path",
                                 "$newip_dir",
                               ],
                               "value")
  if (newip_enhanced) {
    import("../../../dsoftbus_enhance/components/newip/network/network.gni")

    conn_common_src += newip_connection_src
    conn_common_inc += newip_connection_include
  }
} else {
  conn_common_src += [
    "$dsoftbus_root_path/core/connection/common/src/softbus_base_listener_virtual.c",
    "$dsoftbus_root_path/core/connection/common/src/softbus_datahead_transform_virtual.c",
    "$dsoftbus_root_path/core/connection/common/src/softbus_socket_virtual.c",
    "$dsoftbus_root_path/core/connection/common/src/softbus_tcp_socket_virtual.c",
  ]
}
