# Copyright (c) 2022-2024 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/test.gni")
import("../../../../../../dsoftbus.gni")

module_output_path = "dsoftbus/transmission"

ohos_unittest("SoftbusPermissionTest") {
  module_out_path = module_output_path
  sources = [ "unittest/softbus_permission_test.cpp" ]

  include_dirs = [
    "$dsoftbus_root_path/core/common/include",
    "$dsoftbus_root_path/core/common/security/permission/include",
    "$dsoftbus_root_path/adapter/common/include",
  ]

  deps = [
    "$dsoftbus_root_path/core/common:softbus_utils",
    "$dsoftbus_root_path/core/frame:softbus_server",
    "$dsoftbus_root_path/tests/sdk:softbus_client_static",
    "$dsoftbus_root_path/tests/sdk/common:softbus_access_token_test",
  ]
  external_deps = [
    "googletest:gtest_main",
    "hilog:libhilog",
  ]
}

ohos_unittest("SoftbusPermissionACLTest") {
  module_out_path = module_output_path
  sources = [ "unittest/softbus_permission_acl_test.cpp" ]

  include_dirs = [
    "$dsoftbus_root_path/adapter/common/include",
    "$dsoftbus_root_path/core/bus_center/interface",
    "$dsoftbus_root_path/core/common/include",
    "$dsoftbus_root_path/core/common/security/permission/include",
    "$dsoftbus_root_path/core/common/security/permission/standard",
    "$dsoftbus_root_path/core/discovery/interface",
    "$dsoftbus_root_path/core/discovery/manager/include",
  ]

  deps = [
    "$dsoftbus_root_path/core/common:softbus_utils",
    "$dsoftbus_root_path/core/frame:softbus_server",
    "$dsoftbus_root_path/tests/sdk:softbus_client_static",
    "$dsoftbus_root_path/tests/sdk/common:softbus_access_token_test",
  ]
  if (is_standard_system) {
    external_deps = [
      "access_token:libaccesstoken_sdk",
      "access_token:libtoken_setproc",
      "googletest:gtest_main",
      "hilog:libhilog",
      "ipc:ipc_single",
      "samgr:samgr_proxy",
    ]
    if (dsoftbus_access_token_feature) {
      external_deps += [
        "access_token:libaccesstoken_sdk",
        "access_token:libtoken_setproc",
      ]
    }
    if (softbus_os_account) {
      external_deps += [ "os_account:os_account_innerkits" ]
    }
    if (!defined(global_parts_info) ||
        defined(global_parts_info.ability_ability_runtime)) {
      external_deps += [ "ability_runtime:app_manager" ]
    }
  } else {
    external_deps = [
      "c_utils:utils",
      "googletest:gtest_main",
      "hilog:libhilog",
    ]
  }
  if (!defined(global_parts_info) ||
      defined(global_parts_info.deviceprofile_device_info_manager)) {
    external_deps += [
      "device_info_manager:distributed_device_profile_common",
      "device_info_manager:distributed_device_profile_sdk",
    ]
    sources += [ "$dsoftbus_core_path/common/security/permission/standard/access_control.cpp" ]
  } else {
    sources += [ "$dsoftbus_core_path/common/security/permission/standard/access_control_virtual.c" ]
  }
}

group("unittest") {
  testonly = true
  deps = [ ":SoftbusPermissionTest" ]
  if (dsoftbus_access_token_feature) {
    deps += [ ":SoftbusPermissionACLTest" ]
  }
}
