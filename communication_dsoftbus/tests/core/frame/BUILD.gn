# Copyright (c) 2021-2023 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("../../../core/adapter/core_adapter.gni")
import("../../../core/authentication/authentication.gni")
import("../../../core/broadcast/broadcast.gni")
import("../../../core/bus_center/bus_center_server.gni")
import("../../../core/common/security/permission/permission.gni")
import("../../../core/connection/conn.gni")
import("../../../core/discovery/disc.gni")
import("../../../core/transmission/trans.gni")
import("../../../dsoftbus.gni")

dsoftbus_root_path = "../../.."
dsoftbus_feature_product_config_path =
    "${dsoftbus_root_path}/adapter/default_config"
softbus_adapter_config = "${dsoftbus_root_path}/adapter/default_config"

dsoftbus_server_common_src =
    auth_server_src + bus_center_server_src + conn_manager_src +
    disc_server_src + trans_session_src + softbus_permission_src + broadcast_src
dsoftbus_server_common_inc =
    auth_server_inc + bus_center_server_inc + conn_manager_inc +
    disc_server_inc + trans_session_inc + softbus_permission_inc + broadcast_inc
dsoftbus_server_common_deps =
    auth_server_deps + bus_center_server_deps + disc_server_deps +
    conn_manager_common_deps + trans_session_deps + softbus_permission_deps +
    broadcast_deps

config("softbus_frame_test_config") {
  include_dirs = [
    "$dsoftbus_root_path/service/softbus_server/include",
    "$dsoftbus_root_path/core/common/inner_communication",
    "$dsoftbus_root_path/core/common/include",
    "$dsoftbus_root_path/sdk/frame/include",
  ]
}

ohos_executable("softbus_test") {
  sanitize = {
    cfi = true
    cfi_cross_dso = true
    debug = false
  }
  branch_protector_ret = "pac_ret"

  install_enable = true
  sources = [ "softbus_test.c" ]
  configs = [ ":softbus_frame_test_config" ]

  if (is_standard_system) {
    defines += [ "STANDARD_SYSTEM_ENABLE" ]

    deps = [ "$dsoftbus_root_path/tests/sdk:softbus_client_static" ]
    part_name = "softbus_L2"
  }
  subsystem_name = "communication"
}

copy("softbus_permission_json_test") {
  sources = [ "$dsoftbus_core_path/common/security/permission/softbus_trans_permission.json" ]
  outputs = [ "$root_out_dir/etc/softbus_trans_permission.json" ]
}

copy("softbus_server.rc.test") {
  if (use_musl) {
    sources = [ "$dsoftbus_root_path/core/frame/$os_type/init/src/softbus_server_musl.cfg" ]
    outputs = [ "$root_out_dir/etc/softbus_server_musl.cfg" ]
  } else {
    sources = [
      "$dsoftbus_root_path/core/frame/$os_type/init/src/softbus_server.cfg",
    ]
    outputs = [ "$root_out_dir/etc/softbus_server.cfg" ]
  }
}

ohos_static_library("softbus_server_static") {
  sanitize = {
    cfi = true
    cfi_cross_dso = true
    debug = false
  }
  branch_protector_ret = "pac_ret"

  defines += AUTH_SERVER_DEFINES
  defines += DISC_SERVER_DEFINES
  defines += [ "__STDC_FORMAT_MACROS" ]
  include_dirs = dsoftbus_server_common_inc
  include_dirs += [
    "$dsoftbus_dfx_path/interface/include",
    "$dsoftbus_root_path/core/frame/common/include",
    "$dsoftbus_root_path/core/frame/$os_type/client_manager/include",
    "$dsoftbus_root_path/core/frame/$os_type/init/include",
    "$dsoftbus_root_path/core/common/include",
    "$dsoftbus_root_path/interfaces/inner_kits/lnn",
    "$dsoftbus_root_path/interfaces/kits/bus_center",
    "$dsoftbus_root_path/interfaces/kits/common",
    "$dsoftbus_root_path/interfaces/kits/transport",
    "$dsoftbus_root_path/core/common/include",
    "$dsoftbus_feature_product_config_path/spec_config",
  ]
  sources = dsoftbus_server_common_src
  sources += [
    "$dsoftbus_root_path/core/frame/$os_type/client_manager/src/softbus_client_info_manager.cpp",
    "$dsoftbus_root_path/core/frame/$os_type/init/src/if_softbus_server.cpp",
    "$dsoftbus_root_path/core/frame/$os_type/init/src/softbus_server.cpp",
    "$dsoftbus_root_path/core/frame/$os_type/init/src/softbus_server_death_recipient.cpp",
    "$dsoftbus_root_path/core/frame/$os_type/init/src/softbus_server_stub.cpp",
    "$dsoftbus_root_path/core/frame/common/src/softbus_server_frame.c",
    "$dsoftbus_root_path/sdk/frame/$os_type/src/if_softbus_client.cpp",
  ]
  deps = dsoftbus_server_common_deps
  deps += [
    ":softbus_permission_json_test",
    ":softbus_server.rc.test",
    "$dsoftbus_dfx_path/dumper/legacy:softbus_dfx_dump",
    "$dsoftbus_root_path/adapter:softbus_adapter",
    "$dsoftbus_root_path/core/common:softbus_utils",
    "$dsoftbus_root_path/core/connection/wifi_direct_cpp:wifi_direct",
  ]
  external_deps = [ "cJSON:cjson" ]
  external_deps += auth_server_ex_deps
  public_deps = [ "$dsoftbus_dfx_path:softbus_dfx" ]
  dsoftbus_server_common_external_deps =
      bus_center_server_external_deps + disc_server_external_deps +
      softbus_permission_external_deps + trans_session_external_deps +
      conn_manager_external_deps
  if (is_standard_system) {
    external_deps += dsoftbus_server_common_external_deps
    external_deps += [
      "device_auth:deviceauth_sdk",
      "hilog:libhilog",
      "ipc:ipc_single",
      "kv_store:distributeddata_inner",
      "netmanager_base:net_conn_manager_if",
      "safwk:system_ability_fwk",
      "samgr:samgr_proxy",
    ]
    if (dsoftbus_access_token_feature) {
      external_deps += [
        "access_token:libaccesstoken_sdk",
        "access_token:libprivacy_sdk",
        "access_token:libtokenid_sdk",
      ]
    }
    if (!defined(global_parts_info) ||
        defined(global_parts_info.deviceprofile_device_info_manager)) {
      external_deps += [
        "device_info_manager:distributed_device_profile_common",
        "device_info_manager:distributed_device_profile_sdk",
      ]
    }
    if (!defined(global_parts_info) ||
        defined(global_parts_info.bundlemanager_bundle_framework)) {
      external_deps += [
        "bundle_framework:appexecfwk_base",
        "bundle_framework:appexecfwk_core",
      ]
      cflags = [ "-DSUPPORT_BUNDLENAME" ]
    }
    if (!defined(global_parts_info) ||
        defined(global_parts_info.ability_ability_runtime)) {
      cflags += [ "-DSUPPORT_ABILITY_RUNTIME" ]
    }
    if (enhanced) {
      cflags += [ "-DENHANCED_FLAG" ]
      external_deps += [
        "device_certificate_manager:device_cert_mgr_sdk",
        "zlib:libz",
      ]
    }
  }
  if (defined(global_parts_info) &&
      defined(global_parts_info.hmoshiviewdfx_hiview_xcommradar_plugin)) {
    include_dirs += [
      "$dsoftbus_root_path/../../../vendor/huawei/base/hiviewdfx/hiview_plugins/communication_radar_plugin/interfaces/inner_api/communication_radar",
      "$dsoftbus_dfx_path/statistics/include",
    ]
    sources += [
      "$dsoftbus_dfx_path/statistics/instant/bt_statistic.cpp",
      "$dsoftbus_dfx_path/statistics/instant/instant_statistics.cpp",
      "$dsoftbus_dfx_path/statistics/instant/wifi_statistic.cpp",
    ]
    external_deps += [ "hiview_xcommradar_plugin:communication_radar_client" ]
  } else {
    sources += [
      "$dsoftbus_dfx_path/statistics/instant/instant_statistics_virtual.cpp",
    ]
  }
  part_name = "dsoftbus"
  subsystem_name = "communication"
}

group("unittest") {
  testonly = true
  deps = [ "unittest:unittest" ]
}

group("fuzztest") {
  testonly = true
  deps = [ "fuzztest:fuzztest" ]
}
